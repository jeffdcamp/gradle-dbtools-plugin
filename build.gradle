apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'maven-publish'

def groupId = "org.dbtools"
group = groupId
archivesBaseName = "gradle-dbtools-plugin"
version = '7.0.0'

sourceCompatibility = '1.7'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'org.dbtools:dbtools-gen:7.0.0-SNAPSHOT'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

// ./gradlew clean check installLocal
task installLocal(type: Upload) {
    description "Installs the artifacts to the local Maven repository."
    configuration = configurations['archives']
    repositories {
        mavenDeployer {
            repository(url: "file://${System.env.HOME}/.m2/repository/")
            pom.groupId = groupId
            pom.artifactId = archivesBaseName
            pom.version = version
            pom.packaging = 'jar'
            uniqueVersion = false
            // don't put in a timestamp on the filename (other project will not be able to depend on it)
        }
    }
}

// ./gradlew clean check deploy
task deploy(type: Upload) {
    description "Deploys the artifacts to the Nexus repository."
    configuration = configurations['archives']
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'DBTools Gradle Plugin'
                packaging 'jar'
                description 'Gradle plugin for use with dbtools'
                url 'https://github.com/jeffdcamp/gradle-dbtools-plugin'

                scm {
                    connection 'scm:git:git://github.com/jeffdcamp/gradle-dbtools-plugin.git'
                    developerConnection 'scm:git:git@github.com:jeffdcamp/gradle-dbtools-plugin.git'
                    url 'https://github.com/jeffdcamp/gradle-dbtools-plugin'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'jcampbell'
                        name 'Jeff Campbell'
                        email 'jeffdcamp@gmail.com'
                    }
                }
            }
        }
    }
}
